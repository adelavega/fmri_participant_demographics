?participants: participants_n | participants_inline

participants_n.2: _extra_text PARTICIPANTS_NAME n_value _extra_text
participants_inline: _extra_text number _extra_text PARTICIPANTS_NAME _extra_text

participants_details: "(" participants_detail_item (_SEP participants_detail_item)* ")"

?participants_detail_item: participants_subgroup
  | age_info
  | extra_participants_detail

participants_subgroup.1: number SUBGROUP_NAME

?age_info.1: age_mean_std | age_mean | age_median | age_range

age_mean_std: _ALPHA_WORD* NUMBER "±" NUMBER "years" ["old"]

age_mean: [ "mean" ] "age" NUMBER "years" ["old"]

age_median: "median" ["age"] NUMBER "years" ["old"]

age_range:  _ALPHA_WORD* INT _DASH INT "years" ["old"]

extra_participants_detail: _WORD*

_extra_text: (ADJ|_PARENTHESES|_WORD|_SINGLE_RPAREN)*

n_value: LPAREN "n" "=" INT RPAREN

?number: INT
  | text_number

?text_number.1: hundred_text_number | dozen_text_number | TEEN_NAME | UNIT_NAME


hundred_text_number.1: (dozen_text_number | UNIT_NAME) "hundred" [ "and" ] [ dozen_text_number | UNIT_NAME ]

dozen_text_number.1: DOZEN_NAME [ UNIT_NAME ] | TEEN_NAME

UNIT_NAME.1: "one"|"two"|"three"|"four"|"five"|"six"|"seven"|"eight"|"nine"

TEEN_NAME.1: "ten"|"eleven"|"twelve"|"thirteen"|"fourteen"|"fifteen"
    |"sixteen"|"seventeen"|"eighteen"|"nineteen"

DOZEN_NAME.1: "twenty"|"thirty"|"fourty"|"fifty"|"sixty"|"seventy"|"eighty"
    |"ninety"

PARTICIPANTS_NAME.1: "participants"|"subjects"|"controls"|"patients"|"volunteers"
    |"individuals"|"adults"|"children"|"adolescents"
    |"men"|"women"|"males"|"male"|"females"|"female"

SUBGROUP_NAME: "females"|"female"|"males"|"male"|"women"|"woman"|"men"|"man"

ADJ.1: "healthy"
_SEP: ","|";"|"and"
_PARENTHESES: /\([^)]*\)/
_WORD: /\b\w+\b/
_ALPHA_WORD: /\b[^\W\d_]+\b/
_SINGLE_RPAREN: /[)]/
_DASH: "-"|"–"|"—"|"‒"|"―"
LPAREN: "("
RPAREN: ")"
WS: /\s+/
PUNCT: /[^a-zA-Z0-9()]/

%import common (INT, NUMBER)
%ignore WS
%ignore PUNCT