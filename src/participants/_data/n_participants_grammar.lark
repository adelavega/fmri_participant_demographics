?number: INT -> digit_number
    | text_number

text_number.10: hundred_text_number | dozen_text_number | unit_text_number

hundred_text_number.10: hundred_part [dozen_part | unit_part]

hundred_part.10: (dozen_text_number | unit_text_number) "hundred"

dozen_part.10: ["and"] dozen_text_number

unit_part.10: ["and"] UNIT_NAME

dozen_text_number.10: DOZEN_NAME [ UNIT_NAME ] | TEEN_NAME

unit_text_number.10: UNIT_NAME

DOZEN_NAME.10: "twenty"|"thirty"|"fourty"|"fifty"|"sixty"|"seventy"|"eighty"
    |"ninety"

TEEN_NAME.12: "ten"|"eleven"|"twelve"|"thirteen"|"fourteen"|"fifteen"
    |"sixteen"|"seventeen"|"eighteen"|"nineteen"

UNIT_NAME.10: "one"|"two"|"three"|"four"|"five"|"six"|"seven"|"eight"|"nine"


participants: participants_n | participants_inline

participants_n: extra_text [PARTICIPANTS_ADJ short_extra_text] PARTICIPANTS_NAME "(" "n" "=" INT ")"

PARTICIPANTS_ADJ: "healthy"

participants_inline: extra_text number participants_desc [ /(\([^)]*\))/ ]

participants_desc: short_extra_text PARTICIPANTS_NAME

participants_details: "(" participant_detail_item (DETAIL_SEP participant_detail_item)* ")"

DETAIL_SEP: ","|";"|"and"

?participant_detail_item: participants_subgroup_n
  | age_info
  | extra_detail

participants_subgroup_n: number SUBGROUP_NAME

SUBGROUP_NAME: /females?/|/males?/

extra_detail:  /[^),]+/

?age_info.10: age_mean_std | age_mean | age_range

age_mean_std.10: "age" [":"] NUMBER "±" NUMBER "years"

age_mean.10: ["mean"] "age" NUMBER "years"

age_range.10:  ["age"] INT DASH INT "years"

DASH: "-"|"–"|"—"|"‒"|"―"

PARTICIPANTS_NAME: "participants"|"subjects"|"controls"|"patients"|"volunteers"
    |"individuals"|"adults"|"children"|"adolescents"
    |"men"|"women"|"males"|"male"|"females"|"female"

short_extra_text: TOKEN~0..10
extra_text: TOKEN*

TOKEN: /\b\w+\b/ | /\([^)]*\)/ | /\W/
WS: /\s/

%import common (INT, NUMBER)
%ignore WS
%ignore "-"
